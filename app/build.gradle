apply plugin: 'com.android.application'

ext {
    version_sdk = [
            compileSdk: 27,
            minSdk    : 17,
    ]
    version_lib = [
            appcompat: "27.0.0",
            butterknife: "8.8.1",
            netcipher: "2.0.0-alpha1",
    ]
}


android {
    compileSdkVersion version_sdk.compileSdk
    flavorDimensions "default"

    defaultConfig {
        minSdkVersion version_sdk.minSdk
        targetSdkVersion version_sdk.compileSdk
        buildConfigField "boolean", "IS_TEST_BUILD", "false"
        buildConfigField "boolean", "IS_GPLAY_BUILD", "false"
        buildConfigField("String[]", "APPLICATION_LANGUAGES", '{' + getUsedAndroidLanguages().collect {
            "\"${it}\""
        }.join(",") + '}')
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""

        applicationId "com.github.dfa.diaspora_android"
        versionName "1.0.7"
        versionCode 27


        vectorDrawables.useSupportLibrary = true
        resValue 'string', 'app_name', "dandelion*"
        manifestPlaceholders = [appIcon: "@drawable/ic_launcher"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        flavorDefault {
        }
        /*flavorGplay {
            buildConfigField "boolean", "IS_GPLAY_BUILD", "true"
        }*/
        flavorTest {
            applicationId "com.github.dfa.secondlion"
            resValue 'string', 'app_name', "secondlion*"
            manifestPlaceholders = [appIcon: "@drawable/ic_launcher_test"]
            versionCode = Integer.parseInt(new Date().format('yyMMdd'))
            versionName = new Date().format('yyMMdd')
            buildConfigField "boolean", "IS_TEST_BUILD", "true"
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    // Sub-Projects
    //implementation project(':subprojectFromRoot')

    // Jars
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'

    // Android standard libs
    implementation "com.android.support:appcompat-v7:${version_lib.appcompat}"
    implementation "com.android.support:design:${version_lib.appcompat}"
    implementation "com.android.support:support-v4:${version_lib.appcompat}"
    implementation "com.android.support:customtabs:${version_lib.appcompat}"
    implementation "com.android.support:cardview-v7:${version_lib.appcompat}"

    // UI libraries
    implementation "com.github.DASAR:ShiftColorPicker:v0.5"

    // Tool libraries
    implementation "com.jakewharton:butterknife:${version_lib.butterknife}"
    implementation "info.guardianproject.netcipher:netcipher:${version_lib.netcipher}"
    implementation "info.guardianproject.netcipher:netcipher-webkit:${version_lib.netcipher}"

    annotationProcessor "com.jakewharton:butterknife-compiler:${version_lib.butterknife}"
}
